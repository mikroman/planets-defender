0 GOSUB199
1 POKE53280,0:REM BORDER
2 POKE53281,0:REM BACKGROUND
3 POKE649,1:REM 649=MAXIMUM KEYBOARD BUFFER SIZE
4 POKE650,255:REM ALL KEYS WILL REPEAT
5 POKE651,1:REM DELAY BETWEEN KEY REPEATS
6 POKE652,1:REM DELAY UNTIL FIRST KEY REPEAT BEGINS
7 POKE198,0:REM NUMBER OF CHARS WAITING IN BUFFER
8 POKE52,56:REM LOWER THE TOP OF MEMORY
9 POKE56,56:REM SET HIGHEST ADDRESS USED BY BASIC
10 CLR:REM CLEAR VARIABLES/ARRAYS AND SET STRING PTRS
11 GOSUB149:REM SET UP SID
12 GOSUB156:REM PLAY FREQ SWEEP
13 GOSUB203
14 GETA$:REM GET A KEY
15 IFA$<>" "THEN14:REM WAIT FOR SPACE
16 GOSUB218
17 GOTO172:REM CONTINUE SET UP
18 GOSUB229
19 GETA$:REM GET A KEY
20 IFA$=""THEN19:REM WAIT FOR A KEYPRESS
21 DZ=VAL(A$):REM DZ = A NUMBER VALUE OF THE KEYPRESS
22 IFDZ<1THEN19:REM CHECK OUT OF RANGE?
23 IFDZ>7THEN19
24 POKE53280,0:REM BORDER
25 POKE650,129:REM WHICH KEYS WILL REPEAT
26 POKE53281,3:REM BACKGROUND
27 POKE54296,15:REM VOLUME
28 POKE53272,31:REM SELECT WHICH CHARACTER BANK THE VIC SEES
29 GOSUB231
30 GOSUB149:REM SET UP SID
31 FORI=1TO19
32 GOSUB233
33 NEXT
34 GOSUB235
35 B=54272:REM COLOUR RAM PTR (ADDED TO A1)
36 A1=1804:REM SCREEN RAM PTR
37 M1=-40:REM MISSILE MOVEMENT
38 POKEA1,0:REM MISSILE UP
39 POKEA1+B,2:REM MATCHING COLOUR ADDRESS
40 SE=1:REM UFO TRAVEL DISTANCE PER STEP
41 IFC1=0THENM1=-40:REM DIRECTION SETS MISSILE MOVEMENT VARIABLE
42 IFC1=1THENM1=-39
43 IFC1=2THENM1=1
44 IFC1=3THENM1=41
45 IFC1=4THENM1=40
46 IFC1=5THENM1=39
47 IFC1=6THENM1=-1
48 IFC1=7THENM1=-41
49 A1=A1+M1:REM NEW POSITION
50 A3=PEEK(A1):REM WHAT IS THERE NOW?
51 QB=A1-M1
52 A=PEEK(QB)
53 IFA>7THEN55:REM IF IT'S NOT A MISSLE SHAPE
54 POKEQB,29:REM ERASE PREVIOUS MISSILE CHAR
55 IFA1<>A2THEN59:REM NOT A MISSILE/UFO COLLISION?
56 Z=A1:REM IT'S A HIT
57 GOSUB109:REM SHOW FLASH/PLAY EXPLOSION SFX
58 GOTO41:REM START NEXT ROUND
59 IFA3<>29THEN62:REM IF IT'S NOT A BLANK
60 POKEA1,C1:REM MISSILE CHARACTER 0-7
61 POKEA1+B,14:REM TRIANGLE UP
62 IFA3>13THEN66:REM SOLID BLOCK
63 Z=A1:REM HIT THE GROUND/CLOUDS
64 GOSUB99:REM RESET SOME VARS
65 GOSUB109:REM SHOW FLASH/PLAY EXPLOSION SFX
66 IFT2<1THEN69:REM CONTINUE UNLESS IN SPEED BOOST MODE
67 T2=T2-1:REM SPEED BOOST COUNTER
68 GOTO49:REM SKIP INPUT CONTROL WHILE HI-SPEED
69 POKE1104+SP,29:REM BLANK WHERE UFO WAS
70 SP=SP+SE:REM INCREASE UFO'S DISTANCE BY STEP SE
71 POKE1104+SP,8:REM (OTHER)TRIANGLE-UP CHAR
72 POKEB+1104+SP,0:REM MISSLE UP CHAR
73 SE=SE+.04:REM INCREASE UFO TRAVEL DISTANCE PER STEP
74 IFSE>DZTHENSE=DZ:REM DO NOT INCREASE PAST DIFFICULTY LEVEL
75 IFSP<681THEN79:REM IS THE UFO TOO LOW?
76 POKE1104+SP,29:REM BLANK CHAR
77 SP=1:REM RESET UFO PTRS
78 SE=1:REM UFO TRAVEL DISTANCE PER STEP
79 ZX=PEEK(197):REM CURRENT KEYPRESS
80 A=PEEK(56320):REM BIT SELECTION FOR PORT2 JOYSTICK
81 A=NOTA
82 JL=AAND15
83 IFJL=4THEN85:REM WEST
84 IFZX<>47THEN88:REM < KEY(left)
85 C1=C1-1:REM CHANGE MISSILE CHARACTER BASED ON DIRECTION
86 IFC1<0THENC1=7:REM ONLY 8 MISSILE CHARS
87 GOTO41:REM START NEXT ROUND
88 IFJL=8THEN90:REM EAST
89 IFZX<>44THEN93:REM > KEY(right)
90 C1=C1+1:REM CHANGE MISSILE CHARACTER BASED ON DIRECTION
91 IFC1=8THENC1=0:REM ONLY 8 MISSILE CHARS
92 GOTO41:REM START NEXT ROUND
93 IFJL<>1THEN95:REM CHECK FOR A KEYPRESS
94 GOTO96:REM NORTH WAS PRESS ON JOY
95 IFZX<>55THEN97:REM ? KEY(speed boost) WAS NOT PRESSED
96 T2=5:REM SPEED BOOST ENGAGED
97 M1=-41:REM M2 NOT USED. SHOULD BE M1?
98 GOTO41:REM START NEXT ROUND
99 A1=1804:REM RESET MISSILE AND UFO PTRS
100 C1=0:REM MISSILE CHARACTER
101 M1=-40:REM MISSILE MOVEMENT
102 F1=F1-1:REM NUMBER OF MISSILES
103 POKE1904+20+F1,29:REM ERASE THE MISSILE JUST FIRED
104 RETURN
105 A=PEEK(A1)
106 IFA>7THENRETURN
107 POKEA1,29
108 RETURN
109 J2=Z:REM COLLISION POSITION
110 FORJ3=1TO15:REM EXPLOSION CHARS
111 POKEJ2,24:REM EXPLOSION CHAR
112 A=INT(15*RND(1))+1
113 POKEJ2+B,A:REM RANDOM CHAR FROM 1-15
114 NEXT
115 POKEJ2,29:REM BLANK OUT THE EXPLOSION
116 POKEJ2+B,1:REM COLOUR MATCH
117 IFJ2<1063THEN120
118 IFJ2>1823THEN120
119 GOTO123
120 POKEJ2,8:REM TRIANGLE UP
121 POKEJ2+B,1:REM COLOUR MATCH
122 GOTO130
123 G2=G2+1
124 POKE1942-G2,29:REM BLANK
125 GOSUB163:REM NOISE SWEEP
126 GOSUB149:REM RESET SID
127 POKE1064+SP,29:REM BLANK
128 SP=1:REM UFO'S DISTANCE
129 SE=1:REM UFO TRAVEL DISTANCE PER STEP
130 IFG2<>10THEN134:REM CHECK MISSILE COUNT IF NOT 10 UFOS
131 POKE53272,21:REM NORMAL CHARSET CHOSEN
132 GOSUB239
133 GOTO141
134 IFF1<>-20THEN138:REM IF STILL HAS MISSILES
135 POKE53272,21:REM NORMAL CHARSET
136 GOSUB241
137 GOTO141
138 IFG2<>10THEN140:REM STILL HAS UFOS TO DESTROY
139 IFF1=-20THEN141:REM STILL HAS MISSILES TO FIRE
140 RETURN
141 GOSUB156:REM PLAY FREQ SWEEP
142 FORZX=1TO1500:REM DELAY LOOP
143 NEXT
144 GOSUB243
145 GETA$:REM GET A KEYSTROKE
146 IFA$<>" "THEN145:REM WAIT FOR SPACE BAR
147 GOSUB199:REM CLEAR VARIABLES
148 GOTO18:REM SELECT SKILL AND START
149 POKE54296,15:REM SET VOLUME TO 15(HIGH)
150 POKE54276,33:REM VOICE1 CONTROL REGISTER(SAWTOOTH GATE)
151 POKE54277,128:REM AD =128
152 POKE54278,128:REM SR =128
153 POKE54273,3:REM LOW FREQ
154 POKE54272,33:REM HI FREQ
155 RETURN
156 POKE54276,33:REM GATE THE SOUND
157 FORZX=1TO150STEP3:REM SWEEP THE FREQ.
158 POKE54273,ZX:REM LF
159 POKE54272,ZX:REM HF
160 NEXT
161 POKE54276,32:REM TURN OFF THE GATE
162 RETURN
163 POKE54276,129:REM SET NOISE WAVEFORM
164 POKE54296,15:REM MAX VOLUME
165 FORZD=30TO2STEP-1:REM FREQ SWEEP DOWN
166 POKE54273,ZD:REM LF
167 POKE54272,ZD:REM HF
168 NEXT
169 POKE54272,32:REM LF
170 POKE54273,3:REM HF
171 RETURN
172 A=PEEK(56334)
173 A=AAND254
174 POKE56334,A:REM STOP TIMER; NO INTERRUPTS
175 CN=0:REM INTIALIZE CHARACTER NUMBER COUNTER
176 A=PEEK(1)
177 A=AAND251:REM SELECT CHARACTER ROM
178 POKE1,A:REM SELECT CHARACTER ROM
179 FORI=0TO2047:REM 2K NEEDED FOR FULL CHARSET
180 A=PEEK(53248+I)
181 POKEI+14336,A:REM COPY THE CHARSET INTO RAM
182 NEXTI
183 A=PEEK(1)
184 A=AOR4
185 POKE1,A:REM SELECT I/O
186 A=PEEK(56334)
187 A=AOR1
188 POKE56334,A:REM RESTART CIA TIMER
189 FORI=0TO7:REM LOOP THROUGH CHAR DATA
190 READCD:REM TRANSFER A BYTE OF DATA INTO CD
191 IFCD>-1THEN195:REM DONE?
192 I=7
193 NEXTI
194 GOTO18:REM SET SKILL LEVEL AND START GAME
195 POKE14336+8*CN+I,CD:REM INSERT OUR CHARSET PORTION
196 NEXTI
197 CN=CN+1:REM CHARACTER NUMBER COUNTER
198 GOTO189
199 DZ=0:B=0:A1=0:M1=0:P1=0:N1=0:SE=0:I=0:C1=0:M2=0:A2=0:A3=0:T1=0:T2=0:T3=0
200 SP=0:Z=0:ZX=0:JL=0:D2=0:D3=0:SA=0:SF=0:J1=0:J2=0:V=0:WV=0:G2=0:F1=0:ZD=0
201 ZF=0:CN=0:CD=0:S1=0:D1=0:S2=0:A=0:X=0:Y=0:QB=0:REM VARIABLE INITIALIZATION
202 RETURN
203 PRINT"{clear}{down}{space*10}N{cm y*17}M"
204 PRINT"{space*6}{sh space}{space*3}{cm h}planets{space*2}defender{cm n}"
205 PRINT"{space*10}M{cm p*17}N"
206 PRINT"{down}you must defend your planet from a "
207 PRINT"fleet of ufo's by guiding missiles onto"
208 PRINT"them. they accelerate as they come down to bomb your planet."
209 PRINT"you have twenty missiles to shoot down"
210 PRINT"10 of the ufo's. good luck!"
211 PRINT"use the keyboard or joystick in port2"
212 PRINT"{down}you can use the hyperthrust which gives an extra burst of speed but "
213 PRINT"makes the missile difficult to control "
214 PRINT"{down}your planet is destroyed if you run out of missiles"
215 PRINT"if ufo is not destroyed it will return!"
216 PRINT"{down}{space*15}hit space"
217 RETURN
218 PRINT"{clear}{down}{space*8}if using the joystick:"
219 PRINT"{down}{reverse on}west {reverse off} left{space*6}{reverse on}east{reverse off} right"
220 PRINT"{down}{reverse on}north{reverse off} hyperthrust"
221 PRINT"{down*2}{space*8}if using the keyboard:"
222 PRINT"{down}{reverse on}<{reverse off}{space*2}left{space*8}{reverse on}>{reverse off} right"
223 PRINT"{down}{reverse on}?{reverse off} hyperthrust"
224 PRINT"{down}{space*14}please wait"
225 PRINT"{space*10}N{cm y*17}M"
226 PRINT"{sh space}{space*9}{cm h}planets{space*2}defender{cm n}"
227 PRINT"{space*10}M{cm p*17}N"
228 RETURN
229 PRINT"{clear}{down*4}{space*4}set skill level 1=easy{space*2}7=hard:"
230 RETURN
231 PRINT"{clear}{up}{white}hhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhh";
232 RETURN
233 PRINT"{]*40}";
234 RETURN
235 PRINT"{green}hhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhh";
236 PRINT"{yellow}{reverse on}{space*5}missiles{space*17}u.f.o's{space*3}";
237 PRINT"{reverse off}{light blue}@@@@@@@@@@@@@@@@@@@@{space*8}hhhhhhhhhh{space*2}";
238 RETURN
239 PRINT"{clear}{down*4} you saved the planet"
240 RETURN
241 PRINT"{clear}{down*4} no more missiles:ufo's win"
242 RETURN
243 PRINT"{down*2} press space bar to continue"
244 RETURN
245 DATA16,56,56,56,16,56,124,108,1,14,14,14,112,208,48,32,0,192,238,127,238
246 DATA192,0,0,32,48,208,112,14,14,14,1,108,124,56,16,56,56,56,16,4,12,11,14
247 DATA112,112,112,128,0,3,119,254,119,3,0,0,128,112,112,112,14,11,12,4,0,0
248 DATA0,0,24,60,126,255,192,224,192,159,211,145,153,223,16,48,56,56,92,100
249 DATA124,254,254,116,44,56,16,16,16,255,24,60,108,118,124,56,16,255,255,255
250 DATA255,255,255,255,255,255,0,0,0,0,24,60,126,255,128,128,224,248,248,240
251 DATA224,128,255,126,126,60,24,8,0,0,1,7,7,31,15,7,3,1,255,255,255,255,255
252 DATA255,255,255,3,3,0,0,0,0,0,0,0,0,0,0,14,15,7,7,0,0,0,0,0,128,224,192
253 DATA224,224,192,0,0,0,0,0,24,126,129,90,36,24,24,36,137,93,42,212,47,52
254 DATA206,145,5,2,1,0,0,0,0,0,0,0,0,0,0,1,2,5,0,0,0,0,0,128,64,160,128,128
255 DATA224,160,64,128,0,0,0,0,0,0,0,0,0,0,0,0,0,0,92,92,126,126,92,92,0,-1
