100 GOSUB19000
200 POKE53280,0:REM BORDER
300 POKE53281,0:REM BACKGROUND
400 POKE649,1:REM 649=MAXIMUM KEYBOARD BUFFER SIZE
500 POKE650,255:REM ALL KEYS WILL REPEAT
600 POKE651,1:REM DELAY BETWEEN KEY REPEATS
700 POKE652,1:REM DELAY UNTIL FIRST KEY REPEAT BEGINS
800 POKE198,0:REM NUMBER OF CHARS WAITING IN BUFFER
900 POKE52,56:REM LOWER THE TOP OF MEMORY
1000 POKE56,56:REM SET HIGHEST ADDRESS USED BY BASIC
1100 CLR:REM CLEAR VARIABLES/ARRAYS AND SET STRING PTRS
1200 GOSUB14900:REM SET UP SID
1300 GOSUB15600:REM PLAY FREQ SWEEP
1400 GOSUB19400
1500 A$="":REM NULL THE A$
1600 GETA$:REM GET A KEY
1700 IFA$<>" "THEN1500:REM WAIT FOR SPACE
1800 GOSUB20900
1900 GOTO17200:REM CONTINUE SET UP
2000 GOSUB22000
2100 GETA$:REM GET A KEY
2200 IFA$=""THEN2100:REM WAIT FOR A KEYPRESS
2300 DZ=VAL(A$):REM DZ = A NUMBER VALUE OF THE KEYPRESS
2400 IFDZ<1THEN2100:REM CHECK OUT OF RANGE?
2500 IFDZ>7THEN2100
2600 POKE53280,0:REM BORDER
2700 POKE650,129:REM WHICH KEYS WILL REPEAT
2800 POKE53281,3:REM BACKGROUND
2900 POKE54296,15:REM VOLUME
3000 POKE53272,31:REM SELECT WHICH CHARACTER BANK THE VIC SEES
3100 GOSUB22200
3200 GOSUB14900:REM SET UP SID
3300 FORI=1TO19
3400 GOSUB22400
3500 NEXT
3600 GOSUB22600
3700 B=54272:REM COLOUR RAM PTR (ADDED TO A1)
3800 A1=1804:REM SCREEN RAM PTR
3900 M1=-40:REM MISSILE MOVEMENT
4000 POKEA1,0:REM MISSILE UP
4100 POKEA1+B,2:REM MATCHING COLOUR ADDRESS
4200 SE=1:REM UFO TRAVEL DISTANCE PER STEP
4300 IFC1=0THENM1=-40:REM DIRECTION SETS MISSILE MOVEMENT VARIABLE
4400 IFC1=1THENM1=-39
4500 IFC1=2THENM1=1
4600 IFC1=3THENM1=41
4700 IFC1=4THENM1=40
4800 IFC1=5THENM1=39
4900 IFC1=6THENM1=-1
5000 IFC1=7THENM1=-41
5100 A1=A1+M1:REM NEW POSITION
5200 A3=PEEK(A1):REM WHAT IS THERE NOW?
5300 A=PEEK(A1-M1)
5310 IFA>7THEN5500:REM IF IT'S NOT A MISSLE SHAPE
5400 POKEA1-M1,29:REM ERASE PREVIOUS MISSILE CHAR
5500 IFA1<>A2THEN5900:REM NOT A MISSILE/UFO COLLISION?
5600 Z=A1:REM IT'S A HIT
5700 GOSUB10800:REM SHOW FLASH/PLAY EXPLOSION SFX
5800 GOTO4300:REM START NEXT ROUND
5900 IFA3<>29THEN6200:REM IF IT'S NOT A BLANK
6000 POKEA1,C1:REM MISSILE CHARACTER 0-7
6100 POKEA1+B,14:REM TRIANGLE UP
6200 IFA3>13THEN6600:REM SOLID BLOCK
6300 Z=A1:REM HIT THE GROUND/CLOUDS
6400 GOSUB9700:REM RESET SOME VARS
6500 GOSUB10800:REM SHOW FLASH/PLAY EXPLOSION SFX
6600 IFT2<1THEN6900:REM CONTINUE UNLESS IN SPEED BOOST MODE
6700 T2=T2-1:REM SPEED BOOST COUNTER
6800 GOTO5100:REM SKIP INPUT CONTROL WHILE HI-SPEED
6900 POKE1104+SP,29:REM BLANK WHERE UFO WAS
7000 SP=SP+SE:REM INCREASE UFO'S DISTANCE BY STEP SE
7100 POKE1104+SP,8:REM (OTHER)TRIANGLE-UP CHAR
7200 POKEB+1104+SP,0:REM MISSLE UP CHAR
7300 SE=SE+.04:REM INCREASE UFO TRAVEL DISTANCE PER STEP
7400 IFSE>DZTHENSE=DZ:REM DO NOT INCREASE PAST DIFFICULTY LEVEL
7500 IFSP<681THEN7900:REM IS THE UFO TOO LOW?
7600 POKE1104+SP,29:REM BLANK CHAR
7700 SP=1:REM RESET UFO PTRS
7800 SE=1:REM UFO TRAVEL DISTANCE PER STEP
7900 ZX=PEEK(197):REM CURRENT KEYPRESS
8000 A=PEEK(56320):REM BIT SELECTION FOR PORT2 JOYSTICK
8010 A=NOTA
8020 JL=AAND15
8100 IFJL=4THEN8300:REM WEST
8200 IFZX<>47THEN8600:REM < KEY(left)
8300 C1=C1-1:REM CHANGE MISSILE CHARACTER BASED ON DIRECTION
8400 IFC1<0THENC1=7:REM ONLY 8 MISSILE CHARS
8500 GOTO4300:REM START NEXT ROUND
8600 IFJL=8THEN8800:REM EAST
8700 IFZX<>44THEN9100:REM > KEY(right)
8800 C1=C1+1:REM CHANGE MISSILE CHARACTER BASED ON DIRECTION
8900 IFC1=8THENC1=0:REM ONLY 8 MISSILE CHARS
9000 GOTO4300:REM START NEXT ROUND
9100 IFJL<>1THEN9300:REM CHECK FOR A KEYPRESS
9200 GOTO9400:REM NORTH WAS PRESS ON JOY
9300 IFZX<>55THEN9500:REM ? KEY(speed boost) WAS NOT PRESSED
9400 T2=5:REM SPEED BOOST ENGAGED
9500 M1=-41:REM M2 NOT USED. SHOULD BE M1?
9600 GOTO4300:REM START NEXT ROUND
9700 A1=1804:REM RESET MISSILE AND UFO PTRS
9800 C1=0:REM MISSILE CHARACTER
9900 M1=-40:REM MISSILE MOVEMENT
10000 F1=F1-1:REM NUMBER OF MISSILES
10100 POKE1904+20+F1,29:REM BLANK OUT THE MISSILE JUST FIRED
10200 RETURN
10300 A=PEEK(A1)
10310 IFA>7THENRETURN
10320 POKEA1,29
10500 RETURN
10800 J2=Z:REM COLLISION POSITION
11000 FORJ3=1TO15:REM EXPLOSION CHARS
11100 POKEJ2,24:REM EXPLOSION CHAR
11200 A=INT(15*RND(1))+1
11300 POKEJ2+B,A:REM RANDOM CHAR FROM 1-15
11400 NEXT
11500 POKEJ2,29:REM BLANK OUT THE EXPLOSION
11600 POKEJ2+B,1:REM COLOUR MATCH
11700 IFJ2<1063THEN12000
11800 IFJ2>1823THEN12000
11900 GOTO12300
12000 POKEJ2,8:REM TRIANGLE UP
12100 POKEJ2+B,1:REM COLOUR MATCH
12200 GOTO13000
12300 G2=G2+1
12400 POKE1942-G2,29:REM BLANK
12500 GOSUB16300:REM NOISE SWEEP
12600 GOSUB14900:REM RESET SID
12700 POKE1064+SP,29:REM BLANK
12800 SP=1:REM UFO'S DISTANCE
12900 SE=1:REM UFO TRAVEL DISTANCE PER STEP
13000 IFG2<>10THEN13400:REM CHECK MISSILE COUNT IF NOT 10 UFOS
13100 POKE53272,21:REM NORMAL CHARSET CHOSEN
13200 GOSUB23000
13300 GOTO14100
13400 IFF1<>-20THEN13800:REM IF STILL HAS MISSILES
13500 POKE53272,21:REM NORMAL CHARSET
13600 GOSUB23200
13700 GOTO14100
13800 IFG2<>10THEN14000:REM STILL HAS UFOS TO DESTROY
13900 IFF1=-20THEN14100:REM STILL HAS MISSILES TO FIRE
14000 RETURN
14100 GOSUB15600:REM PLAY FREQ SWEEP
14200 FORZX=1TO1500:REM DELAY LOOP
14300 NEXT
14400 GOSUB23400
14500 GETA$:REM GET A KEYSTROKE
14600 IFA$<>" "THEN14500:REM WAIT FOR SPACE BAR
14700 GOSUB19000:REM CLEAR VARIABLES
14800 GOTO2000:REM SELECT SKILL AND START
14900 POKE54296,15:REM SET VOLUME TO 15(HIGH)
15000 POKE54276,33:REM VOICE1 CONTROL REGISTER(SAWTOOTH GATE)
15100 POKE54277,128:REM AD =128
15200 POKE54278,128:REM SR =128
15300 POKE54273,3:REM LOW FREQ
15400 POKE54272,33:REM HI FREQ
15500 RETURN
15600 POKE54276,33:REM GATE THE SOUND
15700 FORZX=1TO150STEP3:REM SWEEP THE FREQ.
15800 POKE54273,ZX:REM LF
15900 POKE54272,ZX:REM HF
16000 NEXT
16100 POKE54276,32:REM TURN OFF THE GATE
16200 RETURN
16300 POKE54276,129:REM SET NOISE WAVEFORM
16400 POKE54296,15:REM MAX VOLUME
16500 FORZD=30TO2STEP-1:REM FREQ SWEEP DOWN
16600 POKE54273,ZD:REM LF
16700 POKE54272,ZD:REM HF
16800 NEXT
16900 POKE54272,32:REM LF
17000 POKE54273,3:REM HF
17100 RETURN
17200 A=PEEK(56334)
17205 A=AAND254
17210 POKE56334,A:REM STOP TIMER; NO INTERRUPTS
17300 CN=0:REM INTIALIZE CHARACTER NUMBER COUNTER
17400 A=PEEK(1)
17405 A=AAND251:REM SELECT CHARACTER ROM
17410 POKE1,A:REM SELECT CHARACTER ROM
17500 FORI=0TO2047:REM 2K NEEDED FOR FULL CHARSET
17600 A=PEEK(53248+I)
17610 POKEI+14336,A:REM COPY THE CHARSET INTO RAM
17700 NEXTI
17800 A=PEEK(1)
17805 A=AOR4
17810 POKE1,A:REM SELECT I/O
17900 A=PEEK(56334)
17905 A=AOR1
17910 POKE56334,A:REM RESTART CIA TIMER
18000 FORI=0TO7:REM LOOP THROUGH CHAR DATA
18100 READCD:REM TRANSFER A BYTE OF DATA INTO CD
18200 IFCD>-1THEN18600:REM DONE?
18300 I=7
18400 NEXTI
18500 GOTO2000:REM SET SKILL LEVEL AND START GAME
18600 POKE14336+8*CN+I,CD:REM INSERT OUR CHARSET PORTION
18700 NEXTI
18800 CN=CN+1:REM CHARACTER NUMBER COUNTER
18900 GOTO18000
19000 DZ=0:B=0:A1=0:M1=0:P1=0:N1=0:SE=0:I=0:C1=0:M2=0:A2=0:A3=0:T1=0:T2=0:T3=0
19100 SP=0:Z=0:ZX=0:JL=0:D2=0:D3=0:SA=0:SF=0:J1=0:J2=0:V=0:WV=0:G2=0:F1=0:ZD=0
19200 ZF=0:CN=0:CD=0:S1=0:D1=0:S2=0:A=0:X=0:Y=0:REM VARIABLE INITIALIZATION
19300 RETURN
19400 PRINT"{clear}{down}{space*10}N{cm y*17}M"
19500 PRINT"{space*6}{sh space}{space*3}{cm h}planets{space*2}defender{cm n}"
19600 PRINT"{space*10}M{cm p*17}N"
19700 PRINT"{down}you must defend your planet from a "
19800 PRINT"fleet of ufo's by guiding missiles onto"
19900 PRINT"them. they accelerate as they come down to bomb your planet."
20000 PRINT"you have twenty missiles to shoot down"
20100 PRINT"10 of the ufo's. good luck!"
20200 PRINT"use the keyboard or joystick in port2"
20300 PRINT"{down}you can use the hyperthrust which gives an extra burst of speed but "
20400 PRINT"makes the missile difficult to control "
20500 PRINT"{down}your planet is destroyed if you run out of missiles"
20600 PRINT"if ufo is not destroyed it will return!"
20700 PRINT"{down}{space*15}hit space"
20800 RETURN
20900 PRINT"{clear}{down}{space*8}if using the joystick:"
21000 PRINT"{down}{reverse on}west {reverse off} left{space*6}{reverse on}east{reverse off} right"
21100 PRINT"{down}{reverse on}north{reverse off} hyperthrust"
21200 PRINT"{down*2}{space*8}if using the keyboard:"
21300 PRINT"{down}{reverse on}<{reverse off}{space*2}left{space*8}{reverse on}>{reverse off} right"
21400 PRINT"{down}{reverse on}?{reverse off} hyperthrust"
21500 PRINT"{down}{space*14}please wait"
21600 PRINT"{space*10}N{cm y*17}M"
21700 PRINT"{sh space}{space*9}{cm h}planets{space*2}defender{cm n}"
21800 PRINT"{space*10}M{cm p*17}N"
21900 RETURN
22000 PRINT"{clear}{down*4}{space*4}set skill level 1=easy{space*2}7=hard:"
22100 RETURN
22200 PRINT"{clear}{up}{white}hhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhh";
22300 RETURN
22400 PRINT"{]*40}";
22500 RETURN
22600 PRINT"{green}hhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhh";
22700 PRINT"{yellow}{reverse on}{space*5}missiles{space*17}u.f.o's{space*3}";
22800 PRINT"{reverse off}{light blue}@@@@@@@@@@@@@@@@@@@@{space*8}hhhhhhhhhh{space*2}";
22900 RETURN
23000 PRINT"{clear}{down*4} you saved the planet"
23100 RETURN
23200 PRINT"{clear}{down*4} no more missiles:ufo's win"
23300 RETURN
23400 PRINT"{down*2} press space bar to continue"
23500 RETURN
23600 DATA16,56,56,56,16,56,124,108,1,14,14,14,112,208,48,32,0,192,238,127,238
23700 DATA192,0,0,32,48,208,112,14,14,14,1,108,124,56,16,56,56,56,16,4,12,11,14
23800 DATA112,112,112,128,0,3,119,254,119,3,0,0,128,112,112,112,14,11,12,4,0,0
23900 DATA0,0,24,60,126,255,192,224,192,159,211,145,153,223,16,48,56,56,92,100
24000 DATA124,254,254,116,44,56,16,16,16,255,24,60,108,118,124,56,16,255,255,255
24100 DATA255,255,255,255,255,255,0,0,0,0,24,60,126,255,128,128,224,248,248,240
24200 DATA224,128,255,126,126,60,24,8,0,0,1,7,7,31,15,7,3,1,255,255,255,255,255
24300 DATA255,255,255,3,3,0,0,0,0,0,0,0,0,0,0,14,15,7,7,0,0,0,0,0,128,224,192
24400 DATA224,224,192,0,0,0,0,0,24,126,129,90,36,24,24,36,137,93,42,212,47,52
24500 DATA206,145,5,2,1,0,0,0,0,0,0,0,0,0,0,1,2,5,0,0,0,0,0,128,64,160,128,128
24600 DATA224,160,64,128,0,0,0,0,0,0,0,0,0,0,0,0,0,0,92,92,126,126,92,92,0,-1
